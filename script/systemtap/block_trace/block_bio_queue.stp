/**
 * # stap -g block_bio_queue.stp
 */

%{
#include <linux/list.h>
#include <linux/blkdev.h>
%}    

function list_request_queue:long (val:long) %{
    struct request *req;
    struct request_queue *q = (struct request_queue *)STAP_ARG_val;
    STAP_RETVALUE = 0;

    list_for_each_entry(req, &q->queue_head, queuelist) {
        STAP_PRINTF("\t\treq:%p bio:%p\n", req, req->bio);
        STAP_RETVALUE ++;
    }
%}

global queue

probe begin {
    printf("probe trace block_bio_queue\n")
}

probe kernel.trace("block_bio_queue") {
    dev_t = $bio->bi_bdev->bd_dev
    major = MAJOR(dev_t)
    minor = MINOR(dev_t)
    bi_sector = $bio->bi_iter->bi_sector
    bi_size = $bio->bi_iter->bi_size
    // req_op defined in blk_types.h 0:REQ_OP_READ 1:REQ_OP_WRITE
    req_op = $bio->bi_opf >> 29
    // 0x8: REQ_SYNC
    flag = $bio->bi_opf & 0x1fffffff
    bi_vcnt = $bio->bi_vcnt
    if (major == 8 && minor == 16) {
        printf("block_bio_queue q:%p bio:%p\n", $q, $bio)
        printf("\tblock_bio_queue (%u, %u) sector:%d size:%d option:0x%x flag:0x%x bi_vcnt:%d comm:%s\n",
               major, minor, bi_sector, bi_size, req_op, flag, bi_vcnt, execname())
        queue = $q
        list_request_queue($q)
    }
}

probe kernel.trace("block_getrq") {
    if ($bio) {
        dev_t = $bio->bi_bdev->bd_dev
        major = MAJOR(dev_t)
        minor = MINOR(dev_t)
        if (major == 8 && minor == 16) {
            printf("block_getrq q:%p bio:%p comm:%s\n", $q, $bio, execname())
        }
        list_request_queue($q)
    }
}

probe kernel.trace("block_rq_insert") {
    if ($q == queue) {
        printf("block_rq_insert q:%p rq:%p bio:%p biotail:%p comm:%s\n",
               $q, $rq, $rq->bio, $rq->biotail, execname())
        printf("\tblock_rq_insert __sector:%d __data_len:%d\n", $rq->__sector, $rq->__data_len)
        list_request_queue($q)
    }
}

probe kernel.trace("block_rq_issue") {
    if ($q == queue) {
        printf("block_rq_issue q:%p rq:%p bio:%p biotail:%p comm:%s\n",
               $q, $rq, $rq->bio, $rq->biotail, execname())
        printf("\tblock_rq_issue request __sector:%d __data_len:%d\n", $rq->__sector, $rq->__data_len)
        if ($rq->__sector >= 0) {
            list_request_queue($q)
        }
    }
}
